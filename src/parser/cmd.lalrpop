use crate::parser::ast::*;
use lalrpop_util::ErrorRecovery;

grammar();

extern {
    type Location = usize;
    type Error = (usize, usize);

    enum ASTKind {
        "&" => ASTKind::Ampersand,
        "|" => ASTKind::Pipe,
        ";" => ASTKind::SemiColon,
        "$" => ASTKind::Dollar,
        "(" => ASTKind::OpenParen,
        ")" => ASTKind::CloseParen,
        "{" => ASTKind::OpenBrace,
        "}" => ASTKind::CloseBrace,
        "=" => ASTKind::Equals,
        "number" => ASTKind::NumberLiteral,
        "." => ASTKind::Dot,
        "," => ASTKind::Comma,
        "literal" => ASTKind::Literal,
        "identifier" => ASTKind::Identifier,
        "\"" => ASTKind::DoubleQuote,
        "error" => ASTKind::Error,
    }
}

pub Delimited : ASTNode = {
    <ll: @L> <left: Delimited> <pipe: Node<SemiColon>> <right: Sequenced> <rr: @R> =>
         ASTNode::new_simple(ll, rr, Delimited::new(), vec![left, pipe, right]),
     <Sequenced>
}

Sequenced : ASTNode = {
    <ll: @L> <left: Sequenced> <pipe: Node<Ampersand>> <right: Piped> <rr: @R> =>
         ASTNode::new_simple(ll, rr, Sequenced::new(), vec![left, pipe, right]),
     <Piped>
}

Piped : ASTNode = {
    <ll: @L> <left: Piped> <pipe: Node<Pipe>> <right: CommandOrFunction> <rr: @R> =>
         ASTNode::new_simple(ll, rr, Piped::new(), vec![left, pipe, right]),
     <CommandOrFunction>
}

CommandOrFunction : ASTNode = {<NodeParent<Command>>, <NodeParent<Function>>}

Command : (Command, Vec<ASTNode>) = {
    <name: Node<CommandName>> <args: NodeParent<CommandArguments>> => {
        (Command::new(), vec![name, args])
    },
    <ll: @L><err: !><rr: @R> => (Command::new(), vec![ASTNode::new_simple(ll, rr, ASTError::new(CommandName::new(), err), vec![])])
}
CommandName : CommandName = {
    <l: Literal> => CommandName::new()
}
CommandArguments : (CommandArguments, Vec<ASTNode>) = {
    <v: Node<Literal>*> => {
        (CommandArguments::new(), v)
    }
}

Function : (Function, Vec<ASTNode>) = {
    <dollar: Node<Dollar>> <value: ValueOrError> => (Function::new(), vec![dollar, value]),
    <dollar: Node<Dollar>> <value: NodeParent<Assignation>> => (Function::new(), vec![dollar, value]),
}

Assignation : (Assignation, Vec<ASTNode>) = {
    <id: Node<VariableName>> <eq: Node<Equals>> <v: ValueOrError> =>
        (Assignation::new(), vec![id, eq, v]),
}

pub PropertyCallNode : ASTNode = { <NodeParent<PropertyCall>> }

PropertyCall : (PropertyCall, Vec<ASTNode>) = {
    <value: Value> <dot: Node<Dot>> <name: Node<PropertyName>> <args: NodeParent<ParenthesizedArgumentsList>>
    => (PropertyCall::new(), vec![value, dot, name, args]),
    <value: Value> <dot: Node<Dot>> <name: Node<PropertyName>> => (PropertyCall::new(), vec![value, dot, name]),
    <name: Node<PropertyName>> <args: NodeParent<ParenthesizedArgumentsList>> => (PropertyCall::new(), vec![name, args]),
    <name: Node<PropertyName>> => (PropertyCall::new(), vec![name]),
}

ParenthesizedArgumentsList : (ParenthesizedArgumentsList, Vec<ASTNode>) = {
    <l: Node<OpenParen>> <args: Value*> <r: Node<CloseParen>> => {
        let mut result = Vec::new();
        result.push(l);
        result.extend(args);
        result.push(r);

        (ParenthesizedArgumentsList::new(), result)
    },

    <l: Node<OpenParen>> <args: Value*> <ll: @L><err: !><rr: @R> => {
        let mut result = Vec::new();
        result.push(l);
        result.extend(args);
        result.push(ASTNode::new_simple(ll, rr, ASTError::new(CloseParen::new(), err), vec![]));

        (ParenthesizedArgumentsList::new(), result)
    }
}


pub Value : ASTNode = {
    <n : StringLiteral> => n,
    <n : Node<NumberLiteral>> => n,
    <n : BracedCommand> => n,
    <n : PropertyCall> => n,
}

#[inline]
ValueOrError : ASTNode = {
    <Value>,
    <ll: @L> <err: !> <rr: @R> => ASTNode::new_simple(ll, rr, ASTError::new(PropertyCall::new(), err), vec![])
}

BracedCommand : ASTNode = {
    <ll: @L> <left: Node<OpenBrace>> <function: Function> <right: Node<CloseBrace>> <rr: @R> => {
        ASTNode::new_simple(ll, rr, BracedCommand::new(), vec![left, function, right])
    },
    <ll: @L> <left: Node<OpenBrace>> <function: Function> <el: @L> <err: !> <er: @R> <rr: @R> => {
        ASTNode::new_simple(ll, rr, BracedCommand::new(), vec![
            left,
            function,
            ASTNode::new_simple(el, er, ASTError::new(CloseBrace::new(), err), vec![])]
        )
    },
}


NodeParent<T> : ASTNode = {
 <l: @L> <v: T> <r: @R> => ASTNode::new(Span::new(l, r), v.0.boxed(), v.1),
};
Node<T> : ASTNode = {
 <l: @L> <v: T> <r: @R> => ASTNode::new(Span::new(l, r), v.boxed(), vec![]),
};

Identifier : Identifier = {
    "identifier" => Identifier::new(),
}

Ampersand : Ampersand = {
    "&" => Ampersand::new(),
}

Pipe : Pipe = {
    "|" => Pipe::new(),
}

SemiColon : SemiColon = {
    ";" => SemiColon::new(),
}

Dollar : Dollar = {
    "$" => Dollar::new(),
}

OpenParen : OpenParen = {
    "(" => OpenParen::new(),
}

CloseParen : CloseParen = {
    ")" => CloseParen::new(),
}

OpenBrace : OpenBrace = {
    "{" => OpenBrace::new(),
}

CloseBrace : CloseBrace = {
    "}" => CloseBrace::new(),
}

StringLiteral : ASTNode = {
    <ll: @L> "\"" Literal "\"" <rr: @R> => ASTNode::new_simple(ll, rr, StringLiteral::new(), vec![]),
    <ll: @L> "\"" Literal <e: !> <rr: @R> => ASTNode::new_simple(ll, rr, ASTError::new(StringLiteral::new(), e), vec![]),
}

PropertyName : PropertyName = {
    "identifier" => PropertyName::new(),
}

NumberLiteral : NumberLiteral = {
    "number" => NumberLiteral::new(),
}

Dot : Dot = {
    "." => Dot::new(),
}

Literal : Literal = {
    "literal" => Literal::new(),
}

Equals : Equals = {
    "=" => Equals::new()
}

VariableName : VariableName = {
    Identifier => VariableName::new()
}
